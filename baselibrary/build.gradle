apply plugin: 'com.android.library'
// 这里添加下面两行代码。
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion  rootProject.ext.android["compileSdkVersion"]


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }
    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

buildscript {
    repositories {
        jcenter()
    }
}
dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    annotationProcessor rootProject.ext.dependencies["glide-annotationProcessor"]
    //support
    api(rootProject.ext.dependencies["support-v4"])
    api(rootProject.ext.dependencies["appcompat"])
    api(rootProject.ext.dependencies["material"])
    api rootProject.ext.dependencies["annotations"]
    testApi rootProject.ext.dependencies["junit"]
    //test
    api rootProject.ext.dependencies["timber"]
    api rootProject.ext.dependencies["constraintlayout"]
    api rootProject.ext.dependencies["androideventbus"]
    api rootProject.ext.dependencies["gson"]
    api rootProject.ext.dependencies["glide"]
    api rootProject.ext.dependencies["glide-okhttp"]
    api rootProject.ext.dependencies["baseUtils"]

    api(rootProject.ext.dependencies["butterknife"]) {
        exclude module: 'support-annotations'
    }


    //下拉刷新
    api rootProject.ext.dependencies["SmartRefreshLayout"]
    api rootProject.ext.dependencies["SmartRefreshHeader"]
    api rootProject.ext.dependencies["SmartRefreshFooter"]


    //轮播图
    api rootProject.ext.dependencies["bga-banner"]

    //不支持androidx 先去掉
    //api rootProject.ext.dependencies["marqueeview"]

    //通用控件
    //  api  rootProject.ext.dependencies["pluginwidget"]
    //multidexmultidex
    api rootProject.ext.dependencies['multidex']

    //通用控件库 不支持AndroidX 先去掉
    api('com.lyb.besttimer:pluginwidget:2.0.0') {
        exclude module: 'recyclerview-v7'
    }
    //不支持AndroidX 去掉
   // api 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    api 'com.orhanobut:logger:2.2.0'

    //不支持AndroidX 也找不到这个项目了，先去掉
    /*api('cn.qqtheme.framework:WheelPicker:1.5.1') {
        exclude group: 'com.android.support'
    }*/

    api rootProject.ext.dependencies["rxlifecycle3"]
    api rootProject.ext.dependencies["rxlifecycle3-android"]
    api rootProject.ext.dependencies["rxlifecycle3-components"]

    //greendao依赖
    api 'org.greenrobot:greendao:3.3.0'
    api 'com.facebook.stetho:stetho:1.5.0'
    api 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    api 'top.zibin:Luban:1.1.8'
    //lifecycle
    api 'androidx.lifecycle:lifecycle-runtime:2.3.0'
    api 'androidx.lifecycle:lifecycle-common-java8:2.3.0'
    api 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    // viewModel
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0"
    api "androidx.fragment:fragment-ktx:1.3.0"
    // liveData
    api "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0"
}

// 定义两个链接，下面会用到。
def siteUrl = 'https://coding.net/u/htjywl/p/BaseProject' // 项目主页。
def gitUrl = 'https://git.coding.net/htjywl/BaseProject.git' // Git仓库的url。
//gradlew clean bintrayUpload
group = "com.htjy.dwm"// 唯一包名,比如compile 'com.ansen.http:okhttpencapsulation:1.0.1'中的com.ansen.http就是这里配置的。gradlew clean
version = "2.1.5"//项目引用的版本号，比如compile 'com.ansen.http:okhttpencapsulation:1.0.1'中的1.0.1就是这里配置的。

install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述，复制我的话， 这里需要修改。
                name 'baselibrary'// 可选，项目名称。
                description 'base library for HTJY Android Project'// 可选，项目描述。
                url siteUrl // 项目主页，这里是引用上面定义好。

                // 软件开源协议，现在一般都是Apache License2.0吧，复制我的，这里不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息，复制我的，这里需要修改。
                developers {
                    developer {
                        id 'htjywl' // 开发者的id。
                        name 'htjywl' // 开发者名字。
                        email 'htjywl@163.com' // 开发者邮箱。
                    }
                }

                // SCM，复制我的，这里不需要修改。
                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}

// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    options.encoding = "utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}

// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user") // Bintray的用户名。
    key = properties.getProperty("bintray.apikey") // Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "baselibrary"  //Repository名字 需要自己在bintray网站上先添加
        name = "baselib"
// 发布到Bintray上的项目名字，这里的名字不是compile 'com.ansen.library:circleimage:1.0.1'中的circleimage。
        userOrg = 'devindeng'//Bintray的组织id
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
    }
}